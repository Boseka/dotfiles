# ZSH COMPONENTS -----------------------------------------------------
autoload -U compinit bashcompinit promptinit colors

# Directory containing zsh completion scripts.
# See https://github.com/zsh-users/zsh-completions/tree/master/src
# This line must be before compinit.
fpath=($fpath ~/.zsh/autocomplete)

# Enable hash-comments at end of line while in REPL mode.
setopt interactivecomments

compinit
bashcompinit
promptinit
colors

## Command history configuration
if [ -z "$HISTFILE" ]; then
    HISTFILE="$HOME/.zsh/history"
fi

HISTSIZE=100000
SAVEHIST=100000

# Show history
case $HIST_STAMPS in
  "mm/dd/yyyy") alias history='fc -fl 1' ;;
  "dd.mm.yyyy") alias history='fc -El 1' ;;
  "yyyy-mm-dd") alias history='fc -il 1' ;;
  *) alias history='fc -l 1' ;;
esac

setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_verify
setopt inc_append_history
setopt share_history

# Enable arrow keys in autocomplete menu.
zstyle ':completion:*' menu select

# PROMPT -------------------------------------------------------------
# For list of built in prompt styles, exec `prompt -l`
# `prompt` requires `autoload -U promptinit && promptinit`
#prompt walters

# Enable git status indicator. See wiki.archlinux.org/index.php/Git#Git_Prompt.
setopt PROMPT_SUBST  # Enable evaluation of functions/substitutions in prompt.
source ~/.zsh/git-prompt.sh

# Left prompt has a leading newline.
PROMPT='
%n@%{$fg[green]%}%m%{$reset_color%} %1d%{$fg[cyan]%}$(__git_ps1 " [%s]")%{$reset_color%} %# '
RPROMPT='%{$fg[green]%}[%?] %d%{$reset_color%}'

# KEY BINDINGS -------------------------------------------------------
# -e for emacs, -v for vim.
bindkey -e

# Fix broken backspace key on some keyboards.
# stty erase ^H

# ^P/^N to go up/down in local history
# ^R still searches in the global history
bindkey "^P" up-line-or-local-history
bindkey "^N" down-line-or-local-history
up-line-or-local-history() {
  zle set-local-history 1
  zle up-line-or-history
  zle set-local-history 0
}
zle -N up-line-or-local-history
down-line-or-local-history() {
  zle set-local-history 1
  zle down-line-or-history
  zle set-local-history 0
}
zle -N down-line-or-local-history

# ENVIRONMENT --------------------------------------------------------
# Paths.
typeset -U path
path=($HOME/bin $path)
path=(/usr/local/bin $path)

# Colored man pages with less.
man() {
  env LESS_TERMCAP_mb=$'\E[01;31m' \
  LESS_TERMCAP_md=$'\E[01;38;5;74m' \
  LESS_TERMCAP_me=$'\E[0m' \
  LESS_TERMCAP_se=$'\E[0m' \
  LESS_TERMCAP_so=$'\E[38;5;246m' \
  LESS_TERMCAP_ue=$'\E[0m' \
  LESS_TERMCAP_us=$'\E[04;38;5;146m' \
  man "$@"
}

# Plugins
# Syntax highlighting for interactive commands.
source ~/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# `bd` to go back to a specific parent directory instead of `cd ../../../..`.
# See github.com/Tarrasch/zsh-bd
source $HOME/.zsh/bd.zsh

# Autocomplete words from other tmux panes.
# See https://blog.plenz.com/2012-01/zsh-complete-words-from-tmux-pane.html
# Usage: ^V for prefix completion and ^V^V for fuzzy completion.
source $HOME/.zsh/_tmux_pane_words.zsh

# z to jump around quickly. See github.com/rupa/z
source $HOME/.zsh/z/z.sh

# Press Enter on an empty buffer (no command) triggers the clear command.
source $HOME/.zsh/enter-to-clear.zsh

source $HOME/.zsh/bell.zsh

source $HOME/.shexports
source $HOME/.shaliases
